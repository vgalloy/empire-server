import net.jokubasdargis.buildtimer.BuildTimerPluginExtension

buildscript {
    dependencies {
        classpath 'net.jokubasdargis.buildtimer:gradle-plugin:0.3.0'
    }
}

plugins {
    // dependencyUpdates
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'net.researchgate.release' version '2.7.0'
}

apply plugin: 'net.jokubasdargis.build-timer'

buildTimer {
    reportAbove = 100L
    sort = BuildTimerPluginExtension.SortOrder.DESC
}

ext {
    javaVersion = '1.8'
    encoding = 'UTF-8'

    checkstyleVersion = '8.10'
    jacocoVersion = '0.8.1'
    pmdVersion = '6.1.0'

    springBootVersion = '2.0.2.RELEASE'
    springFoxVersion = '2.8.0'
    junitVersion = '4.12'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'findbugs'
    apply plugin: 'jacoco'

    group = 'com.vgalloy'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/java')
            }
            resources.srcDir file('src/integration-test/resources')
        }
    }

    checkstyle {
        toolVersion = "$checkstyleVersion"
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        checkstyleTest.enabled = false
        checkstyleIntegrationTest.enabled = false
    }

    pmd {
        toolVersion = "$pmdVersion"
        consoleOutput = true
        ruleSets = []
        ruleSetFiles = rootProject.files('config/pmd/pmd.xml')
        pmdTest.enabled = false
        pmdIntegrationTest.enabled = false
    }

    findbugs {
        excludeFilter = file("$rootProject.projectDir/config/findbugs/findbugs-exception-filter.xml")
        findbugsTest.enabled = false
        findbugsIntegrationTest.enabled = false
    }

    compileJava {
        sourceCompatibility = "$javaVersion"
        targetCompatibility = "$javaVersion"
        options.encoding = "$encoding"
        options.compilerArgs = ['-Xlint:all', '-Werror']
    }

    compileTestJava {
        sourceCompatibility = "$javaVersion"
        targetCompatibility = "$javaVersion"
        options.encoding = "$encoding"
        options.compilerArgs = ['-Xlint:all', '-Werror']
    }

    compileIntegrationTestJava {
        sourceCompatibility = "$javaVersion"
        targetCompatibility = "$javaVersion"
        options.encoding = "$encoding"
        options.compilerArgs = ['-Xlint:all', '-Werror']
    }

    task integrationTest(type: Test) {
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
    }

    jacoco {
        toolVersion = "$jacocoVersion"
    }

    jacocoTestReport {
        dependsOn test
        dependsOn integrationTest

        reports {
            html.enabled = true
        }
        Set<File> reportFiles = new HashSet<>()
        File testReport = file("$buildDir/jacoco/test.exec")
        if(testReport.exists()) {
            reportFiles.add(testReport)
        }
        File integrationTestReport = file("$buildDir/jacoco/integrationTest.exec")
        if(integrationTestReport.exists()) {
            reportFiles.add(integrationTestReport)
        }
        executionData = files(reportFiles)
    }

    integrationTest.mustRunAfter test
    check.dependsOn integrationTest
    check.dependsOn jacocoTestReport
}
