plugins {
    id 'org.springframework.boot' version '2.2.7.RELEASE'
    id 'com.google.cloud.tools.jib' version '2.2.0'
}

apply plugin: 'org.springframework.boot'

configurations {
    apmAgent
}

def elasticsearchApmAgentVersion = '1.6.1'

dependencies {
    api platform(project(':empire-platform'))

    api project(':empire-service')
    api project(':empire-persistence')
    api project(':empire-feature:empire-feature-web')
    api 'org.springdoc:springdoc-openapi-ui'
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.boot:spring-boot-starter-hateoas'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    apmAgent "co.elastic.apm:elastic-apm-agent:$elasticsearchApmAgentVersion"
}

bootJar {
    archiveBaseName = 'empire-server'
    archiveVersion = ''
}

task copyAgent(type: Copy) {
    from configurations.apmAgent
    into "$buildDir/agents"
}

jib {
    from {
        image = 'gcr.io/distroless/java:11'
    }
    extraDirectories {
        paths = file("$buildDir/agents")
    }
    to {
        image = 'empire-server'
    }
    container {
        ports = ['8081']
        jvmFlags = [
            '-javaagent:/elastic-apm-agent-' + "$elasticsearchApmAgentVersion" + '.jar',
            '-Delastic.apm.service_name=empire-server',
            '-Delastic.apm.application_packages=com.vgalloy',
            '-Delastic.apm.server_urls=https://apm.vgalloy.com',
            '-Delastic.apm.verify_server_cert=false',
            '-Delastic.apm.service_version=' + "$project.version"
        ]
    }
}

jibBuildTar.dependsOn copyAgent
build.dependsOn jibBuildTar
