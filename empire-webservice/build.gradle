import org.gradle.api.tasks.Copy

plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'com.google.cloud.tools.jib' version '1.0.2'
}

apply plugin: 'org.springframework.boot'

configurations {
    apmAgent
}

def elasticsearchApmAgentVersion = '1.6.1'

dependencies {
    api project(':empire-service')
    api project(':empire-persistence')
    api project(':empire-feature:empire-feature-web')
    api group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "$springBootVersion"
    api group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: "$springBootVersion"
    api group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: "$springBootVersion"
    api group: 'io.springfox', name: 'springfox-swagger-ui', version: "$springFoxVersion"
    api group: 'io.springfox', name: 'springfox-swagger2', version: "$springFoxVersion"
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "$springBootVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    apmAgent "co.elastic.apm:elastic-apm-agent:$elasticsearchApmAgentVersion"
}

bootJar {
    archiveBaseName = 'empire-server'
    archiveVersion = ''
}

task copyAgent(type: Copy) {
    from configurations.apmAgent
    into "$buildDir/agents"
}

jib {
    from {
        image = 'gcr.io/distroless/java:11'
    }
    extraDirectory = file("$buildDir/agents")
    to {
        image = 'empire-server'
    }
    container {
        ports = ['8081']
        jvmFlags = [
            '-javaagent:/elastic-apm-agent-' + "$elasticsearchApmAgentVersion" + '.jar',
            '-Delastic.apm.service_name=empire-server',
            '-Delastic.apm.application_packages=com.vgalloy',
            '-Delastic.apm.server_urls=https://apm.vgalloy.com',
            '-Delastic.apm.verify_server_cert=false',
            '-Delastic.apm.service_version=' + "$project.version"
        ]
    }
}

jibBuildTar.dependsOn copyAgent
build.dependsOn jibBuildTar
